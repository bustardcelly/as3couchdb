<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   minWidth="955" minHeight="600"
			   creationComplete="handleCreationComplete();">
	
	<!-- This example demonstrates how as3couchdb handles authorization. 								-->
	<!-- as3couchdb works with a CouchSession which is created when a user requests a session cookie. 	-->
	<!-- That cookie is held for the alloted time specified (default 10 minutes like CouchDB).			-->
	<!--																								-->
	<!-- As requests are being made, this cookie is passed along to ensure that the user related to		-->
	<!-- the session has permission. CouchDB handle validation, and it is up to you to deal with		-->
	<!-- user context on th CouchDB side. For simplicity, you can create an administrator and run this	-->
	<!-- example to see how they have access to everything ONLY once they have started a session.		-->
	<!--																								-->
	<!-- When a session cookie expires and a request is made, a new one is acquired and the pending 	-->
	<!-- pending request is forwarded along.															-->
	
	<fx:Script>
		<![CDATA[
			import com.custardbelly.as3couchdb.core.CouchDatabase;
			import com.custardbelly.as3couchdb.core.CouchDocument;
			import com.custardbelly.as3couchdb.core.CouchServiceFault;
			import com.custardbelly.as3couchdb.core.CouchServiceResult;
			import com.custardbelly.as3couchdb.core.CouchSession;
			import com.custardbelly.as3couchdb.core.CouchUser;
			import com.custardbelly.as3couchdb.enum.CouchActionType;
			import com.custardbelly.as3couchdb.event.CouchEvent;
			import com.custardbelly.couchdb.example.alert.LogInPanel;
			import com.custardbelly.couchdb.example.model.ContactDatabase;
			import com.custardbelly.couchdb.example.model.ContactDocument;
			import com.custardbelly.couchdb.example.model.ContactUser;
			import com.custardbelly.couchdb.example.serialize.ContactDocumentReader;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			
			private var _login:LogInPanel;
			private var _requiresLogin:Boolean = true;
			
			private var _database:ContactDatabase;
			private var _document:ContactDocument;
			private var _contactReader:ContactDocumentReader;
			
			[Bindable]
			public var contactsCollection:ArrayCollection;
			
			// Event handler for creation complete, shows login.
			private function handleCreationComplete():void
			{
				if( !_requiresLogin ) return;
				
				// Create login.
				if( _login == null )
				{
					_login = new LogInPanel();
					_login.addEventListener( LogInPanel.SUBMIT, handleLoginSubmit, false, 0, true );
				}
				// Add to PopUpManager.
				PopUpManager.addPopUp( _login, this, true );
				PopUpManager.centerPopUp( _login );
			}
			
			// Event handler for submit on login.
			private function handleLoginSubmit( evt:Event ):void
			{
				_login.showMessage( "Loading..." );
				var username:String = _login.username;
				var password:String = _login.password;
				var user:CouchUser = new ContactUser( username, password );
				user.addEventListener( CouchActionType.LOGIN, handleContactLogin );
				user.addEventListener( CouchEvent.FAULT, handleLoginFault );
				user.login();
			}
			
			// Fault in create of session.
			private function handleLoginFault( evt:CouchEvent ):void
			{
				var fault:CouchServiceFault = evt.data as CouchServiceFault;
				_login.showMessage( "[ERROR] " + fault.message );
			}
			// Successful create of session.
			private function handleContactLogin( evt:CouchEvent ):void
			{
				_login.showMessage( "Complete." );
				PopUpManager.removePopUp( _login );
				
				_contactReader = new ContactDocumentReader();
			}
			
			private function loadDatabase():void
			{
				// Creates Database if not already exists.
				_database = new ContactDatabase();
				_database.addEventListener( CouchActionType.CREATE, handleDatabaseRead, false, 0, true );
				_database.addEventListener( CouchEvent.FAULT, handleDatabaseFault, false, 0, true );
				_database.addEventListener( CouchActionType.READ_DOCUMENTS, handleAllDocumentsRead, false, 0, true );
				_database.createIfNotExist();
			}
			
			private function loadAllContacts():void
			{
				// Loads all documents related to database.
				_database.getAllDocuments();
			}
			
			private function createDocument( firstName:String, lastName:String, email:String ):void
			{
				// Creates a new contact document.
				_document = new ContactDocument();
				_document.firstName = firstName;
				_document.lastName = lastName;
				_document.email = email;
				_document.addEventListener( CouchActionType.CREATE, handleDocumentCreate, false, 0, true );
				_document.addEventListener( CouchEvent.FAULT, handleDocumentFault, false, 0, true );
				_document.create();
			}
			 
			// Resolves a generic object returned from _all_docs to a ContactDocument instance. 
			protected function resolveToContact( value:Object ):ContactDocument
			{
				var contact:ContactDocument;
				try
				{
					// Try and fill document from result.
					// If faulted, document returned is not related to a ContactDocument.
					contact = _contactReader.createDocumentFromResult( value, getQualifiedClassName( ContactDocument ) ) as ContactDocument;
				}
				catch( e:Error )
				{
					// Could not resolve generic object returned from _all_docs as a ContactDocument.
				}
				return contact;
			}
			
			// Event handler for submit of new contact
			private function handleSubmit():void
			{
				createDocument( firstNameField.text, lastNameField.text, emailField.text );
			}
			
			// Event handler for read in of database from createIfNotExists.
			private function handleDatabaseRead( evt:CouchEvent ):void
			{
				var result:CouchServiceResult = evt.data as CouchServiceResult;
				var theDatabaseRead:CouchDatabase = result.data as CouchDatabase;
				// theDatabaseRead is the same as the class member _database.
				// Just showing how you can access the database from the event.
				
				contactForm.enabled = true;
				listForm.enabled = true;
				loadDatabaseButton.enabled = false;
			}
			
			// Event handler of read in of all documents from database.
			private function handleAllDocumentsRead( evt:CouchEvent ):void
			{
				var result:CouchServiceResult = evt.data as CouchServiceResult;
				var list:Array = result.data as Array;
				
				var i:int;
				var contact:ContactDocument;
				contactsCollection = new ArrayCollection();
				for( i = 0; i < list.length; i++ )
				{
					contact = resolveToContact( list[i] );
					if( contact )
						contactsCollection.addItem( contact );
				}
				contactsList.dataProvider = contactsCollection;
			}
			
			// Event handler for creation of document.
			private function handleDocumentCreate( evt:CouchEvent ):void
			{
				var result:CouchServiceResult = evt.data as CouchServiceResult;
				var document:CouchDocument = result.data as CouchDocument;
				// document is the same as the class member _document.
				// Just showing how you can access the document from the event.
				
				// Clear out form.
				firstNameField.text = "";
				lastNameField.text = "";
				emailField.text = "";
				// Reload through service.
				loadAllContacts();
			}
			
			// Event handler for fault in service related to database.
			private function handleDatabaseFault( evt:CouchEvent ):void
			{
				var fault:CouchServiceFault = evt.data as CouchServiceFault;
				Alert.show( fault.message, "Database Error:" );
			}
			
			// Event hanlder for fault in service related to document.
			private function handleDocumentFault( evt:CouchEvent ):void
			{
				var fault:CouchServiceFault = evt.data as CouchServiceFault;
				Alert.show( fault.message, "Document Error:" );
			}
		]]>
	</fx:Script>
	
	<s:layout>
		<s:VerticalLayout />
	</s:layout>
	
	<fx:Binding source="contactsCollection" destination="contactsList.dataProvider" />
	
	<s:BorderContainer width="300" height="200">
		
		<s:layout>
			<s:VerticalLayout paddingLeft="5" paddingRight="5"
							  paddingTop="5" paddingBottom="5"
							  />
		</s:layout>
		
		<!-- Database controls. -->
		<s:HGroup verticalAlign="bottom">
			<s:Label text="Contacts Database." />
			<s:Button id="loadDatabaseButton" 
					  label="load database" 
					  click="loadDatabase();" 
					  />
		</s:HGroup>
		
		<!-- Contacts list -->
		<s:VGroup id="listForm" width="100%" height="100%" enabled="false">
			<s:List id="contactsList" 
					width="100%" height="100%"
					itemRenderer="com.custardbelly.couchdb.example.view.ContactItemRenderer">
				<s:layout>
					<s:VerticalLayout paddingLeft="5" paddingRight="5" 
									  paddingTop="5" paddingBottom="5" 
									  />
				</s:layout>
			</s:List>
			<s:Button label="load contacts" click="loadAllContacts();" />
		</s:VGroup>
		
	</s:BorderContainer>
	
	<!-- Contact Form -->
	<s:BorderContainer id="contactForm" width="300" enabled="false">
		
		<s:layout>
			<s:VerticalLayout paddingLeft="5" paddingRight="5"
							  paddingTop="5" paddingBottom="5"
							  horizontalAlign="center"
							  />
		</s:layout>
		
		<s:Label text="Add Contact:" />
		<s:HGroup width="100%" verticalAlign="bottom">
			<s:Label width="65" text="First Name:" />
			<s:TextInput id="firstNameField" width="100%" />
		</s:HGroup>
		<s:HGroup width="100%" verticalAlign="bottom">
			<s:Label width="65" text="Last Name:" />
			<s:TextInput id="lastNameField" width="100%" />
		</s:HGroup>
		<s:HGroup width="100%" verticalAlign="bottom">
			<s:Label width="65" text="Email:" />
			<s:TextInput id="emailField" width="100%" />
		</s:HGroup>
		
		<s:Button label="submit" click="handleSubmit();" />
		
	</s:BorderContainer>
</s:Application>
